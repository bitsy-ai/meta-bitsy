require conf/distro/include/rpi.inc
require conf/distro/include/no-static-libs.inc
require conf/distro/include/security_flags.inc

DISTRO ?= "bitsy"
DISTRO_NAME ?= "BitsyLinux"
YOCTO_VERSION = "4.0.1"
YOCTO_CODENAME = "Kirkstone"
DISTRO_VERSION ?= "0.1.0"
DISTRO_CODENAME ?= "Amber"
SDK_VENDOR = "-bitsysdk"
SDK_VERSION = "${@d.getVar('DISTRO_VERSION').replace('snapshot-${METADATA_REVISION}', 'snapshot')}"
SDK_VERSION[vardepvalue] = "${SDK_VERSION}"
HOME_URL = "https://linux.bitsy.ai"
BUG_REPORT_URL = "https://github.com/bitsy-ai/meta-bitsy/issues"
DISTRO_PRETTY = "${DISTRO_NAME} ${DISTRO_VERSION}${@' (%s)' % DISTRO_CODENAME if 'DISTRO_CODENAME' in d else ''}"
OS_RELEASE_FIELDS = "\
    ID ID_LIKE NAME VERSION VERSION_ID PRETTY_NAME DISTRO_CODENAME \
    HOME_URL BUG_REPORT_URL YOCTO_VERSION YOCTO_CODENAME SDK_VERSION VARIANT VARIANT_ID \
"
BUILD_ID = "${@time.strftime('%Y-%m-%dT%H:%M:%S',time.localtime())}"

MAINTAINER = "Leigh Johnson <leigh@bitsy.ai>"

TARGET_VENDOR = "-bitsy"
LOCALCONF_VERSION = "2"

BITSY_DEFAULT_DISTRO_FEATURES = "\
    acl \
    argp \
    bitsy-overlayfs \
    bluetooth \
    ext4 \
    ipv4 \
    ipv6 \
    keyboard \
    largefile \
    seccomp \
    systemd \
    usbgadget \
    usbhost \
    vfat \
    wifi \
    xattrs \
    zeroconf \
"

LICENSE = "AGPLv3"
DESCRIPTION = "A console-only image with read-only root filesystem and writeable data partition"

# required to use both overlayfs-etc and package-management features
# see note: https://git.yoctoproject.org/poky/plain/meta/classes/overlayfs-etc.bbclass
CMDLINE:append = "\
    init=/sbin/preinit \
"

BITSY_DEFAULT_EXTRA_RDEPENDS = ""

BITSY_DEFAULT_EXTRA_RRECOMMENDS = "kernel-module-af-packet"
DISTRO_FEATURES ?= "\
    ${DISTRO_FEATURES_DEFAULT} \
    ${BITSY_DEFAULT_DISTRO_FEATURES} \
"

# EXTRA_IMAGE_FEATURES += "ptest-pkgs"

PREFERRED_VERSION_linux-raspberrypi = "5.15.%"


SDK_NAME = "${DISTRO}-${TCLIBC}-${SDKMACHINE}-${IMAGE_BASENAME}-${TUNE_PKGARCH}-${MACHINE}"
SDKPATHINSTALL = "/opt/${DISTRO}/${SDK_VERSION}"

DISTRO_EXTRA_RDEPENDS  += "${BITSY_DEFAULT_EXTRA_RDEPENDS}"
DISTRO_EXTRA_RRECOMMENDS += "${BITSY_DEFAULT_EXTRA_RRECOMMENDS}"

BB_SIGNATURE_HANDLER ?= "OEEquivHash"
BB_HASHSERVE ??= "auto"
TCLIBCAPPEND = ""

# meta-raspberrypi
ENABLE_UART = "1"

# Note also that Linux supports multiple consoles for output - but only one for input
# the last named (or default if none named) from the kernel commandline specifies which is used for input, and all are used for output
CMDLINE:append = "\
    fsck.repair=yes \
    systemd.show_status=1 \
    splash \
"

# Use systemd for system initialization
DISTRO_FEATURES:append = " systemd"
DISTRO_FEATURES_BACKFILL_CONSIDERED:append = " sysvinit"
VIRTUAL-RUNTIME_init_manager = "systemd"
VIRTUAL-RUNTIME_initscripts = "systemd-compat-units"
VIRTUAL-RUNTIME_login_manager = "shadow-base"
VIRTUAL-RUNTIME_dev_manager = "systemd"

PREFERRED_VERSION_systemd = "251.1"

INHERIT += "issue-txt"
